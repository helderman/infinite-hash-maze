; testtape.z80asm - MZ-700 program to test tape interface
; To display object code (for typing over into MZ-700):
; z80asm -o - -l testtape.z80asm | od -Ax -tx1 -w8
; The object code is relocatable; put it wherever you like.
; To generate some test audio:
; sox -b8 -c1 -r8k -n test.wav synth 10 0 0 50 square 200 vol 1 dcshift 0
; where 50 = duty cycle (percentage; change to determine polarity).
; Volume [0,1] and dcshift [-1,1]: to determine threshold and sensitivity.

	ORG	1200H

START:	CALL	069FH		; motor on (1Z-013A)
FOREVR:	LD	HL,0D800H+999	; bottom right of screen
FRAME:	LD	(HL),2		; background red
	LD	A,(0E002H)	; bit 5 = tape read
	RLCA
	RLCA
	RLCA			; CF = tape read
	SBC	A,A		; 00h = low, FFh = high
	AND	06H
	OR	30H
	LD	(0E007H),A	; 8253 counter 0 mode 0 or 3
	LD	(HL),A		; background black or yellow
	DEC	HL
	BIT	3,H		; HL < 0D800H?
	JR	NZ,FRAME	; next character position
	JR	FOREVR		; next frame

	END

; **  MONITOR 1Z-013A  **
; *M1200
; (enter the 30 bytes listed below, one at a time)
; (Shift-Break)
; *D1200121E
; 1200 CD 9F 06 21 E7 DB 36 02
; 1208 3A 02 E0 07 07 07 9F E6
; 1210 06 F6 30 32 07 E0 77 2B
; 1218 CB 5C 20 EA 18 E5
; *J1200
