; ------------------------------------------------------------
; Smooth Scrolling Hash Maze for Sharp MZ-700
;
; Maze scrolls at 2 pixels per frame (or 4 when holding shift)
; at 50 fps, without flicker.
; Maze has no border, but repeats itself every 65536 cells
; in either direction.
; ------------------------------------------------------------

; MZ-700 timing (according to service manual):
; CPU clock frequency: 3.546875 MHz
; Horizontal period: 64.05603 us = 227.20 clock cycles
; - Raster scan:     36.088   us = 128.00 clock cycles
; - HBLANK:          27.96803 us =  99.20 clock cycles
; Any attempt to access video RAM during raster scan
; will halt the CPU until the start of the next VBLANK period.
; This goes for all 312 horizontal periods within each frame.
; Even during VBLANK!!!

; MZ-700 memory map: video RAM (40x25 character matrix)
VRAM:	EQU	0D000H	; start address (characters)
VRAMC:	EQU	0D800H	; start address (colors)
COLS:	EQU	40	; screen width (number of columns)
ROWS:	EQU	25	; screen height (number of rows)
SPACE:	EQU	00H	; character code: space
LEFT2:	EQU	37H	; character code: left 2 pixels set
LEFT4:	EQU	7BH	; character code: left 4 pixels set
RIGHT2:	EQU	3FH	; character code: right 2 pixels set
RIGHT4:	EQU	3BH	; character code: right 4 pixels set
UPPER2:	EQU	36H	; character code: upper 2 pixels set
UPPER4:	EQU	7AH	; character code: upper 4 pixels set
LOWER2:	EQU	3EH	; character code: lower 2 pixels set
LOWER4:	EQU	3AH	; character code: lower 4 pixels set
MCOLOR:	EQU	40H	; color code: maze
ICOLOR:	EQU	04H	; color code: maze, inverted

; 4K CGROM
PL1:	EQU	0EAH	; character code: player, top left
PL2:	EQU	0EBH	; character code: player, top right
PL3:	EQU	0ECH	; character code: player, bottom left
PL4:	EQU	0EDH	; character code: player, bottom right
PCOLOR:	EQU	0D0H	; color code: player

; 2K CGROM
;PL1:	EQU	0FEH	; character code: player, top left
;PL2:	EQU	0FDH	; character code: player, top right
;PL3:	EQU	0FBH	; character code: player, bottom left
;PL4:	EQU	0F7H	; character code: player, bottom right
;PCOLOR:	EQU	50H	; color code: player

; MZ-700 memory map: memory-mapped I/O (keyboard)
PPI:	EQU	0E000H	; PPI 8255
ARROWS:	EQU	0F7H	; PPI port A output for keyboard scanline #8
SHBRK:	EQU	0F8H	; PPI port A output for keyboard scanline #9

; MZ-700 system calls
EXIT:	EQU	00ADH	; return to monitor 1Z-013A
VBLANK:	EQU	0DA6H	; wait until start of vertical blanking period

; Hash steps for horizontal walls
HLEFT:	EQU	-563
HRIGHT:	EQU	563
HDOWN:	EQU	-761
HUP:	EQU	761

; Hash steps for vertical walls
VLEFT:	EQU	-1409
VRIGHT:	EQU	1409
VDOWN:	EQU	-397
VUP:	EQU	397

; Initial position in maze
POSX:	EQU	554
POSY:	EQU	2505

; ---------------------------------
; Macros
; ---------------------------------

; LDAIYL - workaround for unrecognized LD A,IYL
; Size: 2 bytes

LDAIYL:	MACRO
	DEFB	0FDH	; prefix for IY
	LD	A,L
	ENDM

; HASHI - hash and increment
; in:     BC = post-increment for seed
; in/out: HL = seed
; out:    PO = wall, PE = no wall
; registers clobbered: AF, HL
; Size: 3 bytes

HASHI:	MACRO
	LD	A,L
	XOR	H
	ADD	HL,BC
	ENDM

; PAINT - paint maze on screen, row by row (from the bottom up)
; out: IX = video RAM address (color), player position

PAINT:	MACRO
	LD	BC,5*HDOWN
	LD	DE,5*VDOWN
	CALL	MOVHV
	LD	DE,VRAMC+COLS*ROWS
.OUTER:	PUSH	DE
	CALL	CLRWS
	LD	HL,YCURR
	INC	(HL)
	CALL	MOVY
	CALL	EDGEU
	POP	DE
	LD	HL,WORKSP+COLS+5
	LD	B,COLS
	CALL	VBLANK
.INNER:	DEC	DE
	DEC	HL
	LD	A,(DE)
	SET	2,D
	LD	(DE),A		; save original color code
	RES	2,D
	RES	3,D
	LD	A,(DE)
	SET	2,D
	LD	(DE),A		; save original character code
	RES	2,D
	XOR	A		; A=SPACE
	LD	(DE),A		; put new character code
	SET	3,D
	LD	A,(HL)
	LD	(DE),A		; put new color code
	DJNZ	.INNER
	LD	A,E
	OR	A
	JR	NZ,.OUTER
	LD	A,PCOLOR
	LD	HL,VRAM+499
	LD	(HL),PL1
	INC	L
	LD	(HL),PL2
	SET	3,H
	LD	(HL),A
	DEC	L
	LD	(HL),A
	LD	HL,VRAM+539
	LD	(HL),PL3
	INC	L
	LD	(HL),PL4
	SET	3,H
	LD	(HL),A
	DEC	L
	LD	(HL),A
	LD	IX,VRAMC+458	; center of screen
	ENDM

; UNDO - restore original video RAM contents, row by row (from the top down)

UNDO:	MACRO
	LD	DE,VRAMC
	LD	C,ROWS
.OUTER:	LD	B,COLS
	CALL	VBLANK
.INNER:	SET	2,D
	LD	A,(DE)
	RES	2,D
	LD	(DE),A		; restore original color code
	RES	3,D
	SET	2,D
	LD	A,(DE)
	RES	2,D
	LD	(DE),A		; restore original character code
	SET	3,D
	INC	DE
	DJNZ	.INNER
	DEC	C
	JR	NZ,.OUTER
	ENDM

; HCHAR - scroll one character horizontally
; in:     HL = video RAM address (colors)
;         C  = new character code
; in/out: A  = neighboring old color code
; registers clobbered: AF
; 11 bytes; 33 or 51 cycles

HCHAR:	MACRO
	XOR	(HL)
	AND	4
	LD	A,(HL)
	JR	Z,.SKIP
	RES	3,H
	LD	(HL),C
	SET	3,H
.SKIP:	ENDM

; VCHAR - scroll one character vertically
; in: HL = video RAM address (colors)
;     DE = comparison address (video RAM or workspace)
;     C  = new character code
; registers clobbered: AF
; 11 bytes; 33 or 51 cycles

VCHAR:	MACRO
	LD	A,(DE)
	XOR	(HL)
	AND	4
	JR	Z,.SKIP
	RES	3,H
	LD	(HL),C
	SET	3,H
.SKIP:	ENDM

; VROW - scroll one or two rows vertically
; in/out: HL = video RAM address (colors)
;         DE = comparison address (video RAM or workspace)
; in:     C  = new character code
;         B  = 20 for one row, 40 for two rows
; Registers clobbered: AF, B, DE, HL (makes B=0)
; Size: 24 bytes
; Video RAM access in the two VCHAR together spans max 98 cycles;
; should fit in a single HBLANK.
; This means VROW takes max 20 horizontal periods per row of characters.
; SCROD and SCROU only update 10 rows per frame,
; so VROW will occupy max 200 horizontal periods per frame.
; UPDATE: in reality it takes longer. Needs investigation!

VROW:	MACRO
.LOOP:	VCHAR
	INC	E		; equivalent to INC DE because E is even
	INC	L		; equivalent to INC HL because L is even
	VCHAR
	INC	DE
	INC	HL
	DJNZ	.LOOP
	ENDM

; SKIP3 - skip 3 rows (typically following VROW)
; in:     B  = 0
; in/out: DE = video RAM address
;         HL = another video RAM address
; Registers clobbered: AF, DE, HL
; Size: 8 bytes

SKIP3:	MACRO
	LD	A,C		; save C
	LD	C,COLS*3	; BC=120 (demands B=0)
	ADD	HL,BC
	EX	DE,HL
	ADD	HL,BC
	EX	DE,HL
	LD	C,A		; restore C
	ENDM

; REPLCD - replace character and color (D)
; in: HL = video RAM address (characters)
;     A  = old character code
;     C  = new character code
;     D  = new color code
; registers clobbered: F
; 9 bytes; 19 or 44 cycles

REPLCD:	MACRO
	CP	(HL)
	JR	NZ,.SKIP
	LD	(HL),C
	SET	3,H
	LD	(HL),D
	RES	3,H
.SKIP:	ENDM

; REPLCE - replace character and color (E)
; in: HL = video RAM address (characters)
;     A  = old character code
;     C  = new character code
;     E  = new color code
; registers clobbered: F
; 9 bytes; 19 or 44 cycles

REPLCE:	MACRO
	CP	(HL)
	JR	NZ,.SKIP
	LD	(HL),C
	SET	3,H
	LD	(HL),E
	RES	3,H
.SKIP:	ENDM

; REROWD - replace characters and colors (D), entire row
; in/out: HL = video RAM address (characters)
; in:     A  = old character code
;         C  = new character code
;         D  = new color code
; Registers clobbered: F, B, HL (B is made 0)
; Size: 24 bytes
; The 2 REPLCD together span max 92 cycles; should fit in a single HBLANK.
; This means REROWD takes max 20 horizontal periods.
; VPIX calls REROWD 5 times per frame, and REROWE too.
; so VPIX will occupy max 200 horizontal periods per frame.
; UPDATE: in reality it takes longer. Needs investigation!

REROWD:	MACRO
	LD	B,COLS/2
.LOOP:	REPLCD
	INC	L		; equivalent to INC HL because L is even
	REPLCD
	INC	HL
	DJNZ	.LOOP
	ENDM

; REROWE - replace characters and colors (E), entire row
; in/out: HL = video RAM address (characters)
; in:     A  = old character code
;         C  = new character code
;         E  = new color code
; Registers clobbered: F, B, HL (B is made 0)
; Size: 24 bytes
; The 2 REPLCE together span max 92 cycles; these fit in a single HBLANK.
; This means REROWE takes max 20 horizontal periods.
; VPIX calls REROWE 5 times per frame, and REROWD too.
; so VPIX will occupy max 200 horizontal periods per frame.
; UPDATE: in reality it takes longer. Needs investigation!

REROWE:	MACRO
	LD	B,COLS/2
.LOOP:	REPLCE
	INC	L		; equivalent to INC HL because L is even
	REPLCE
	INC	HL
	DJNZ	.LOOP
	ENDM

; SCROR - scroll maze to the right (player moving left)
; in: C = replacement character
; registers clobbered: AF, B, DE, HL, IY
; Video RAM access in the two HCHAR together spans max 102 cycles;
; might just fit in a single HBLANK.
; Iterated 200 times, they occupy 200 horizontal periods per frame.
; UPDATE: in reality it takes longer. Needs investigation!

SCROR:	MACRO
	LD	IY,WORKSP+5
	LD	DE,4
	LD	HL,VRAMC
	LD	A,MCOLOR
	CALL	VBLANK
	LD	A,(XCURR)
	OR	A		; if (XCURR)=0
	JR	Z,.L3
	CPL
	ADD	A,5
	LD	L,A		; HL=VRAMC+4-(XCURR)
.L1:	LD	A,(IY+0)
	INC	IY
	LD	B,8
.L2:	HCHAR
	INC	HL
	HCHAR
	ADD	HL,DE
	DJNZ	.L2
	LDAIYL
	CP	WORKSP+ROWS+5 & 0FFH
	JR	NZ,.L1
	JR	.L5
.L3:	LD	A,(IY+0)
	INC	IY
	LD	B,8
.L4:	HCHAR
	ADD	HL,DE
	HCHAR
	INC	HL
	DJNZ	.L4
	LDAIYL
	CP	WORKSP+ROWS+5 & 0FFH
	JR	NZ,.L3
.L5:	ENDM

; SCROL - scroll maze to the left (player moving right)
; in: C = replacement character
; Registers clobbered: AF, B, DE, HL, IY
; Video RAM access in the two HCHAR together spans max 102 cycles;
; might just fit in a single HBLANK.
; Iterated 200 times, they occupy 200 horizontal periods per frame.
; UPDATE: in reality it takes longer. Needs investigation!

SCROL:	MACRO
	LD	IY,WORKSP+5
	LD	DE,-4
	LD	HL,VRAMC+COLS
	LD	A,MCOLOR
	CALL	VBLANK
	LD	A,(XCURR)
	OR	A		; if (XCURR)=0
	JR	Z,.L3
	NEG
	ADD	A,L
	LD	L,A		; HL=VRAMC+40-(XCURR)
.L1:	LD	A,(IY+0)
	INC	IY
	LD	B,8
.L2:	HCHAR
	DEC	HL
	HCHAR
	ADD	HL,DE
	DJNZ	.L2
	LD	A,C		; save C
	LD	C,2*COLS	; BC=80 (B was already zero)
	ADD	HL,BC
	LD	C,A		; restore C
	LDAIYL
	CP	WORKSP+ROWS+5 & 0FFH
	JR	NZ,.L1
	JR	.L5
.L3:	LD	A,(IY+0)
	INC	IY
	LD	DE,-4
	LD	B,8
.L4:	DEC	HL
	HCHAR
	ADD	HL,DE
	HCHAR
	DJNZ	.L4
	LD	A,C		; save C
	LD	C,2*COLS	; BC=80 (B was already zero)
	ADD	HL,BC
	LD	C,A		; restore C
	LDAIYL
	CP	WORKSP+ROWS+5 & 0FFH
	JR	NZ,.L3
.L5:	ENDM

; SCROD - scroll maze down (player moving up)
; in: C = replacement character	(UPPER2 or UPPER4)
; registers clobbered: AF, B, DE, HL

SCROD:	MACRO
	LD	HL,VRAMC
	LD	A,MCOLOR
	CALL	VBLANK
	LD	A,(YCURR)
	SUB	2
	JR	NC,.L1
	CALL	VROW1E
	LD	DE,VRAMC
	LD	A,(YCURR)
	LD	B,A		; B=0 (required at .L4)
	OR	A
	JR	Z,.L4
	LD	B,COLS/2
	JR	.L3
.L1:	LD	E,A
	ADD	A,A
	ADD	A,A
	ADD	A,E
	ADD	A,A
	ADD	A,A
	ADD	A,A
	LD	E,A		; DE=VRAMC+COLS*((YCURR)-2)
	LD	D,H
	ADD	A,COLS
	LD	L,A		; HL=VRAMC+COLS*((YCURR)-1)
.L2:	LD	B,COLS
.L3:	VROW
.L4:	SKIP3
	LD	A,D
	CP	3+(VRAMC>>8)	; while DE<VRAMC+768
	JR	C,.L2
	LD	B,COLS
	LD	A,E		; DE=VRAMC+920/960/800/840/880
	RLCA
	JR	NC,.L5		; skip if DE<VRAMC+896 i.e. (YCURR)=2/3/4
	LD	B,COLS/2
	RLCA
.L5:	CALL	NC,VROW2	; skip if (YCURR)=1
	ENDM

; ---------------------------------
; Code
; ---------------------------------

	ORG	4000H

START:	PAINT
POLL:	CALL	CLRWS
	LD	HL,PPI
	LD	(HL),SHBRK
	INC	L		; (need a tiny delay before fetching the keys)
	LD	BC,POLL
	LD	A,(HL)		; BC-----S (binary): break, ctrl, shift
	LD	(FAST),A
	AND	81H		; shift+break
	JR	NZ,POLL1
	UNDO
	JP	EXIT
POLL1:	DEC	L
	LD	(HL),ARROWS
	INC	L		; (need a tiny delay before fetching the keys)
	PUSH	BC		; push return address (main loop)
	LD	A,(HL)		; --UDRL-- (binary): up, down, right, left
	OR	0C3H		; neutralize non-arrow keys
	LD	C,A
	CPL			; now 1 = pressed, 0 = released
	LD	B,A
	LD	HL,KSTATE
	AND	(HL)		; if new key pressed, use it
	LD	(HL),C
	INC	HL
	JR	NZ,SWITCH
	LD	A,B
	AND	(HL)		; try the key that was used last time
	JR	NZ,SWITCH
	LD	A,B		; if that key was released, try another key
SWITCH:	LD	(HL),A
	LD	B,2*MCOLOR	; for collision detect
	RLCA
	RLCA
	RLCA
	JP	C,MOVU		; up arrow key
	RLCA
	JP	C,MOVD		; down arrow key
	RLCA
	JP	C,MOVR		; right arrow key
	RLCA
	RET	NC
	; fall through to MOVL; left arrow key

; MOVL - player moves left (walls move right)

MOVL:	LD	A,(IX+40)
	ADD	A,(IX+80)
	CP	B
	RET	NZ	; blocked by wall
	CALL	EDGEL
	LD	BC,LEFT2*100H+LEFT4	; B=LEFT2, C=LEFT4
	CALL	SPEED
	SCROR
	LD	DE,MCOLOR*100H+ICOLOR	; D=MCOLOR, E=ICOLOR
	BIT	2,C			; NZ for LEFT2, Z for LEFT4
	JR	Z,MOVL1
	LD	B,RIGHT4
	CALL	HPIX
	LD	B,RIGHT2
	CALL	HPIX
MOVL1:	CALL	HPIX0
	LD	HL,XCURR
	DEC	(HL)
	; fall through to MOVX

; MOVX - change player position horizontally

MOVX:	LD	A,(HL)
	SUB	5
	RET	C
	LD	BC,HRIGHT
	LD	DE,VRIGHT
	JR	Z,MOVXY
	LD	BC,HLEFT
	LD	DE,VLEFT
MOVXY4:	LD	A,4
MOVXY:	LD	(HL),A
MOVHV:	LD	HL,(HCURR)
	ADD	HL,BC
	LD	(HCURR),HL
	LD	HL,(VCURR)
	ADD	HL,DE
	LD	(VCURR),HL
	RET

; MOVD - player moves down (walls move up)

MOVD:	LD	A,(IX+121)
	ADD	A,(IX+122)
	CP	B
	RET	NZ	; blocked by wall
	CALL	EDGED
	LD	BC,LOWER2*100H+LOWER4	; B=LOWER2, C=LOWER4
	CALL	SPEED
	CALL	SCROU
	LD	DE,ICOLOR*100H+MCOLOR	; D=ICOLOR, E=MCOLOR
	BIT	2,C			; NZ for LOWER2, Z for LOWER4
	JR	Z,MOVD1
	LD	B,UPPER4
	CALL	VPIX
	LD	B,UPPER2
	CALL	VPIX
MOVD1:	CALL	VPIX0
	LD	HL,YCURR
	DEC	(HL)
	; fall through to MOVY

; MOVY - change player position vertically

MOVY:	LD	A,(HL)
	SUB	5
	RET	C
	LD	BC,HUP
	LD	DE,VUP
	JR	Z,MOVXY
	LD	BC,HDOWN
	LD	DE,VDOWN
	JR	MOVXY4

; MOVR - player moves right (walls move left)
; Must be called with HL = XCURR-1

MOVR:	LD	A,(IX+43)
	ADD	A,(IX+83)
	CP	B
	RET	NZ	; blocked by wall
	INC	HL	; HL=XCURR
	INC	(HL)
	CALL	MOVX
	CALL	EDGER
	LD	BC,RIGHT2*100H+RIGHT4	; B=RIGHT2, C=RIGHT4
	CALL	SPEED
	SCROL
	LD	DE,ICOLOR*100H+MCOLOR	; D=ICOLOR, E=MCOLOR
	BIT	2,C			; NZ for RIGHT2, Z for RIGHT4
	JR	Z,HPIX0
	LD	B,LEFT4
	CALL	HPIX
	LD	B,LEFT2
	CALL	HPIX
	; fall through to HPIX0

; HPIX - scroll horizontally in steps of 2 or 4 pixels
; in: C = search, B = replace, D = left color code, E = right color code
; out: C = replace (to chain to next scroll step)
; registers clobbered: AF, BC, HL

HPIX0:	LD	B,SPACE
HPIX:	LD	HL,VRAM
	CALL	VBLANK
	LD	A,(XCURR)
	CPL
	ADD	A,5
	LD	L,A		; HL=VRAM+4-(XCURR)
	LD	A,C		; A=search
	LD	C,B		; C=replace
	LD	B,4*ROWS
	BIT	2,L		; if (XCURR)=0
	RES	2,L		; then HL=VRAM
	JR	NZ,HPIX3	; and skip the first column
HPIX2:	REPLCD
	INC	HL
HPIX3:	REPLCE
	INC	HL
	INC	HL
	INC	HL
	INC	HL
	REPLCD
	INC	HL
	REPLCE
	INC	HL
	INC	HL
	INC	HL
	INC	HL
	DJNZ	HPIX2
	LD	B,A
	LD	A,L
	CP	COLS*ROWS-1 & 0FFH	; if (XCURR)=0
	RET	NZ
	LD	A,B
	CP	(HL)
	RET	NZ
	LD	(HL),C
	SET	3,H
	LD	(HL),D
	RES	3,H
	RET

; MOVU - player moves up (walls move down)
; Must be called with HL = YCURR-2

MOVU:	LD	A,(IX+1)
	ADD	A,(IX+2)
	CP	B
	RET	NZ	; blocked by wall
	INC	HL
	INC	HL	; HL=YCURR
	INC	(HL)
	CALL	MOVY
	CALL	EDGEU
	LD	BC,UPPER2*100H+UPPER4	; B=UPPER2, C=UPPER4
	CALL	SPEED
	SCROD
	LD	DE,MCOLOR*100H+ICOLOR	; D=MCOLOR, E=ICOLOR
	BIT	2,C			; NZ for UPPER2, Z for UPPER4
	JR	Z,VPIX0
	LD	B,LOWER4
	CALL	VPIX
	LD	B,LOWER2
	CALL	VPIX
	; fall through to VPIX0

; VPIX - scroll vertically in steps of 2 or 4 pixels
; in: C = search, B = replace, D = left color code, E = right color code
; out: C = replace (to chain to next scroll step)
; registers clobbered: AF, BC, HL

VPIX0:	LD	B,SPACE
VPIX:	LD	HL,VRAM
	CALL	VBLANK
	LD	A,(YCURR)
	OR	A		; if (YCURR)=0
	JR	NZ,VPIX1
	LD	A,C		; A=search
	LD	C,B		; C=replace
	LD	B,ROWS/5
	PUSH	BC
	JR	VPIX3
VPIX1:	DEC	A
	LD	L,A
	ADD	A,A
	ADD	A,A
	ADD	A,L
	ADD	A,A
	ADD	A,A
	ADD	A,A
	LD	L,A
	LD	A,C		; A=search
	LD	C,B		; C=replace
	LD	B,ROWS/5
VPIX2:	PUSH	BC
	REROWD
VPIX3:	REROWE
	LD	C,COLS*3	; BC=120 (B is already zero)
	ADD	HL,BC
	POP	BC
	DJNZ	VPIX2
	LD	B,A
	LD	A,L
	CP	COLS*(ROWS-1) & 0FFH	; if (YCURR)=0
	RET	NZ
	LD	A,B
	REROWD
	RET

; SCROU - scroll maze up (player moving down)
; in: C = replacement character
; registers clobbered: AF, B, DE, HL

SCROU:	LD	DE,VRAMC+COLS
	LD	H,D
	LD	A,MCOLOR
	LD	B,COLS/2
	CALL	VBLANK
	LD	A,(YCURR)
	LD	L,A
	OR	A		; if (YCURR)=0
	JR	Z,SCROU2	; HL=VRAMC, DE=VRAMC+COLS
	ADD	A,A
	ADD	A,A
	ADD	A,L
	ADD	A,A
	ADD	A,A
	ADD	A,A
	LD	L,A
	SUB	E
	EX	DE,HL		; DE=VRAMC+COLS*(YCURR)
	LD	L,A		; HL=VRAMC+COLS*(YCURR)-COLS
SCROU1:	LD	B,COLS
SCROU2:	VROW
	SKIP3
	LD	A,H
	CP	3+(VRAMC>>8)	; while HL<VRAMC+768
	JR	C,SCROU1
	LD	B,COLS
	LD	A,L		; HL=VRAMC+960/800/840/880/920
	RLCA
	JR	NC,VROW2	; skip if HL<VRAMC+896 i.e. (YCURR)=1/2/3
	RLCA
	CALL	NC,VROW1	; call if (YCURR)=4, skip if (YCURR)=0
	; fall through to VROW1E

; VROW1E - vertically scroll a single row at edge
; VROW1  - vertically scroll a single row
; VROW2  - vertically scroll two rows (requires B=COLS)

VROW1E:	LD	DE,WORKSP+5
VROW1:	LD	B,COLS/2
VROW2:	VROW
	RET

; CLRWS - clear workspace
; registers clobbered: A, B, HL

CLRWS:	LD	HL,WORKSP+5
	LD	B,COLS		; 5+5 off-screen bytes are irrelevant
	LD	A,MCOLOR
CLRWS1:	LD	(HL),A
	INC	HL
	DJNZ	CLRWS1
	RET

; EDGEL - calculate off-screen colors at left edge
; registers clobbered: AF, BC, DE, HL

EDGEL:	LD	HL,WORKSP+ROWS+5
	LD	A,(YCURR)
	LD	C,A
	LD	B,0
	ADD	HL,BC
	EX	DE,HL		; DE=WORKSP+ROWS+5+(YCURR)
	LD	HL,(HCURR)
	LD	BC,4*HLEFT+HDOWN
	ADD	HL,BC
	LD	BC,HUP
	CALL	CROSS
	LD	A,(XCURR)
	OR	A
	RET	NZ
	LD	BC,HLEFT+5*HDOWN
	ADD	HL,BC
	LD	BC,HUP
	CALL	CROSS
	LD	BC,4*VLEFT+2*VDOWN
	JR	EDGELR

; EDGER - calculate off-screen colors at right edge
; registers clobbered: AF, BC, DE, HL

EDGER:	LD	HL,WORKSP+ROWS+5
	LD	A,(YCURR)
	LD	C,A
	LD	B,0
	ADD	HL,BC
	EX	DE,HL		; DE=WORKSP+ROWS+5+(YCURR)
	LD	HL,(HCURR)
	LD	BC,4*HRIGHT+HDOWN
	ADD	HL,BC
	LD	BC,HUP
	CALL	CROSS
	LD	A,(XCURR)
	OR	A
	RET	NZ
	LD	BC,HLEFT+5*HDOWN
	ADD	HL,BC
	LD	BC,HUP
	CALL	CROSS
	LD	BC,4*VRIGHT+2*VDOWN
EDGELR:	LD	HL,(VCURR)
	ADD	HL,BC
	LD	BC,VUP
	JR	PARAL

; EDGEU - calculate off-screen colors at top edge
; registers clobbered: AF, BC, DE, HL

EDGEU:	LD	HL,WORKSP+COLS+10
	LD	A,(XCURR)
	CPL
	LD	C,A
	LD	B,-1
	ADD	HL,BC
	EX	DE,HL		; HL=WORKSP+COLS+9-(XCURR)
	LD	HL,(VCURR)
	LD	BC,4*VRIGHT+3*VUP
	ADD	HL,BC
	LD	BC,VLEFT
	CALL	CROSS
	LD	A,(YCURR)
	OR	A
	RET	NZ
	LD	BC,8*VRIGHT+VDOWN
	ADD	HL,BC
	LD	BC,VLEFT
	CALL	CROSS
	LD	BC,4*HRIGHT+3*HUP
	JR	EDGEUD

; EDGED - calculate off-screen colors at bottom edge
; registers clobbered: AF, BC, DE, HL

EDGED:	LD	HL,WORKSP+COLS+10
	LD	A,(XCURR)
	CPL
	LD	C,A
	LD	B,-1
	ADD	HL,BC
	EX	DE,HL		; HL=WORKSP+COLS+9-(XCURR)
	LD	HL,(VCURR)
	LD	BC,4*VRIGHT+2*VDOWN
	ADD	HL,BC
	LD	BC,VLEFT
	CALL	CROSS
	LD	A,(YCURR)
	OR	A
	RET	NZ
	LD	BC,8*VRIGHT+VDOWN
	ADD	HL,BC
	LD	BC,VLEFT
	CALL	CROSS
	LD	BC,4*HRIGHT+2*HDOWN
EDGEUD:	LD	HL,(HCURR)
	ADD	HL,BC
	LD	BC,HLEFT
	; fall through to PARAL

; PARAL - set walls parallel with edge
; in:     BC = post-increment for seed
; in/out: HL = seed
;         DE = workspace address
; registers clobbered: AF, DE, HL

PARAL:	HASHI
	JP	PO,PARAL1
	DEC	DE
	DEC	DE
	DEC	DE
	DEC	DE
	DEC	DE
	JR	PARAL2
PARAL1:	LD	A,ICOLOR
	LD	(DE),A
	DEC	DE
	LD	(DE),A
	DEC	DE
	LD	(DE),A
	DEC	DE
	LD	(DE),A
	DEC	DE
	LD	(DE),A
	DEC	DE
	LD	(DE),A
PARAL2:	LD	A,E
	CP	WORKSP+5 & 0FFH
	JR	NC,PARAL
	RET

; CROSS - set crossing walls
; in:     BC = post-increment for seed
; in/out: HL = seed
;         DE = workspace address
; registers clobbered: AF, HL

CROSS:	PUSH	DE
CROSS1:	DEC	DE
	DEC	DE
	DEC	DE
	DEC	DE
	DEC	DE
	HASHI
	JP	PE,CROSS2
	LD	A,ICOLOR
	LD	(DE),A
CROSS2:	LD	A,E
	CP	WORKSP+10 & 0FFH
	JR	NC,CROSS1
	POP	DE
	RET

; SPEED - set speed
; in:  B = 2-pixel character code
;      C = 4-pixel character code
; out: C = either one of these character codes
; registers clobbered: F, C

SPEED:	LD	A,(FAST)
	RRCA
	RET	NC
	LD	C,B
	RET

; ---------------------------------
; Initialized variables
; ---------------------------------

; KSTATE - keyboard status
; bits 5/4/3/2 are arrow keys up/down/right/left

KSTATE:	DEFB	-1	; keyboard state, last time (0 = pressed)
	DEFB	0	; key that took effect last time (1 = pressed)

; XCURR - current X position, sub-cell
;
; 0: ----+----+----+----+----+----+----+----+
; 1: ---+----+----+----+----+----+----+----+-
; 2: --+----+----+----+-PL-+----+----+----+--
; 3: -+----+----+----+----+----+----+----+---
; 4: +----+----+----+----+----+----+----+----

XCURR:	DEFB	2

; YCURR - current Y position, sub-cell
;
;  0   1   2   3   4
; -+-  |   |   |   |
;  |  -+-  |   |   |
;  |   |  -+-  |   |
;  |   |   |  -+-  |
;  |   |   |   |  -+-
; -+-  |   |   |   |
;  |  -+-  |   |   |
;  |   |  -+-  |   |
;  |   |   |  -+-  |
;  |   |   |   |  -+-
; -+-  |   |   |   |
;  |  -+-  |   |   |
;  PL  |  -+-  |   |
;  PL  |   |  -+-  |
;  |   |   |   |  -+-
; -+-  |   |   |   |
;  |  -+-  |   |   |
;  |   |  -+-  |   |
;  |   |   |  -+-  |
;  |   |   |   |  -+-
; -+-  |   |   |   |
;  |  -+-  |   |   |
;  |   |  -+-  |   |
;  |   |   |  -+-  |
;  |   |   |   |  -+-

YCURR:	DEFB	0

; HCURR - current position, hashed for horizontal walls

HCURR:	DEFW	HRIGHT*POSX+HUP*POSY & 0FFFFH

; VCURR - current position, hashed for vertical walls

VCURR:	DEFW	VRIGHT*POSX+VUP*POSY & 0FFFFH

; ---------------------------------
; Uninitialized variables
; ---------------------------------

; FAST - if bit 0 is 0, run faster
; (currently unused)

FAST:	DEFS	+1

; WORKSP - workspace

WORKSP:	DEFS	+50

	END
